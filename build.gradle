/*
 * Copyright 2022 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

group = 'cd.go.contrib.task.powershell'
version = '1.0.1-SNAPSHOT' // x-release-please-version

gocdPlugin {
    id            = 'cd.go.contrib.task.powershell'
    version       = project.version
    pluginVersion = project.version
    goCdVersion   = '21.2.0'
    name          = 'Powershell Task Plugin'
    description   = 'GoCD task plugin to run PowerShell scripts.'
    vendorName    = 'Volusion'
    vendorUrl     = 'https://github.com/volusion/gocd-powershell-task-plugin'

    githubRepo {
        owner = System.getenv('GITHUB_USER') ?: 'volusion'
        repo = 'gocd-powershell-task-plugin'
        token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
    }

    pluginProject = project

    prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
    assetsToRelease = [project.tasks.findByName('jar')]
}

repositories {
    mavenCentral()
    mavenLocal()
}

java {
    sourceCompatibility = JavaVersion.VERSION_15
    targetCompatibility = JavaVersion.VERSION_15
}

ext {
    deps = [
        gocdPluginApi: 'cd.go.plugin:go-plugin-api:25.2.0',
    ]

    versions = project.ext.deps.collectEntries { lib, libGav -> [lib, libGav.split(':').last()] }
}

dependencies {
    compileOnly project.deps.gocdPluginApi
    implementation group: 'cd.go.plugin.base', name: 'gocd-plugin-base', version: '1.0.4'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.13.1' // Updated to latest stable
    implementation group: 'commons-io', name: 'commons-io', version: '2.19.0' // Updated to latest stable

    testImplementation platform('org.junit:junit-bom:5.12.2')
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.3'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.18.0'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '3.0' // Common version for Hamcrest with JUnit 5
//    testImplementation group: 'org.jsoup', name: 'jsoup', version: '1.20.1'
    testImplementation project.deps.gocdPluginApi
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.3'
    testImplementation 'uk.org.webcompere:system-stubs-jupiter:2.1.8'
}

test {
    useJUnitPlatform()
}

jar {
    from(configurations.runtimeClasspath) {
        into "lib/"
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath
}
